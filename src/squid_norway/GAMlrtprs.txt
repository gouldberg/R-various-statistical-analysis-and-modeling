
model{
    #1A. Priors regression parameters
    for (i in 1:M)  {beta[i] ~ dnorm(0, 0.0001) }  
    for (i in 1:Mz) {u[i] ~ dnorm(0, tau.u )  }
    b ~ dnorm(0, 0.0001)
   
    #1B. Prior for variance for epsilon
    num   ~ dnorm(0, 0.0016) 
    denom ~ dnorm(0, 1)
    sigma <- abs(num / denom) 
    tau   <- 1 / (sigma * sigma)
       
    #Priors for variance random intercept u for smoother
    num.u   ~ dnorm(0, 0.0016) 
    denom.u ~ dnorm(0, 1)
    sigma.u <- abs(num.u / denom.u) 
    tau.u   <- 1 / (sigma.u * sigma.u)
     
    #2. Likelihood
    for (i in 1:N) {
       Y[i]  ~  dnorm(mu[i], tau)
       mu[i] <- inprod(beta[], Xcov[i,]) + F1[i]         
       F1[i] <- Xspl[i] * b + inprod(Zspl[i,], u[])  #Smoother
              
       #3. Discrepancy measures   
       E[i] <- Y[i] - mu[i]
    }     
 }

